plugins {
    id 'java'
    id 'idea'
}

group 'app.saikat'
version '1.0.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

task copyToLib(type: Copy) {
    into "$buildDir/libs/separate-jars"
    from configurations.runtimeClasspath
}

task copyProj(type: Copy) {
  from "$buildDir/libs/" + jar.archiveFileName.get()
  into "$buildDir/libs/separate-jars"
}

task fatJar(type: Jar) {
    classifier = 'fat'
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    destinationDirectory = file("$buildDir/libs/fat")
    with jar
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

jar.finalizedBy(copyToLib)
jar.finalizedBy(fatJar)
jar.dependsOn(copyProj)

test {
    useJUnit()
    failFast = true
    maxParallelForks = 5
}

dependencies {
    implementation project(':ConfigurationManagement')
    implementation project(':PojoCollections')
    implementation project(':GsonManagement')
    implementation project(':ThreadManagement')
    implementation project(':DIManagement')
    implementation project(':Annotations')


    implementation group: 'com.google.guava', name: 'guava', version: '28.2-jre'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.12.1'

    testImplementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.12.1'    
    testImplementation group: 'junit', name: 'junit', version: '4.12'
}
